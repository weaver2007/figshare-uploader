Import('qt_env')

env = qt_env.Clone()

# The initial hash sign means "relative to the top-level directory".
googletest_framework_root = "#/ext/googletest-release-1.8.0"

googletest_include_paths = [
    googletest_framework_root + "/googletest",
    googletest_framework_root + "/googletest/include",
    googletest_framework_root + "/googlemock",
    googletest_framework_root + "/googlemock/include"
]

gtest_all_path = googletest_framework_root + "/googletest/src/gtest-all.cc"
gmock_all_path = googletest_framework_root + "/googlemock/src/gmock-all.cc"

source_dir = '#/src'

env.Append(CPPPATH=googletest_include_paths)
env.Append(CPPPATH=source_dir)
env.Append(LIBS=['pthread', 'upgradedfortnight'])
env.Append(LIBPATH=source_dir)

env.Program(
    'unit_tests',
    ['test_harness.cc', 'http_getter_test.cc', gtest_all_path, gmock_all_path]
)
